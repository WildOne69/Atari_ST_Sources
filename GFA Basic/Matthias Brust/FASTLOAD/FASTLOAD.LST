' Konvertiert 9 u. 10-Sektor-Disketten
' in ein einfaches Fastload-Format
' von Christian Roth u. Matthias Brust
' Sprache: GFA-BASIC V3.X
'
ALERT 1,"Bitte zu konvertierende|   Disk einlegen! ",1," OK ",dummy|
drive|=GEMDOS(25)      !akt. Laufwerk holen
puffer$=SPACE$(10000)  !Sektor-Puffer
puffer2$=SPACE$(10000) !Format-Puffer
ret%=XBIOS(8,L:V:puffer$,L:0,drive|,1,0,0,1) !Bootsektor holen
IF ret%=0
  secs|=BYTE{V:puffer$+24} !Sektoren pro Track
  IF secs|<11              !nur 9 u. 10 Sekt.
    sides|=BYTE{V:puffer$+26} !Anzahl d. Seiten
    ' holen und Trackanzahl berechnen
    tracks|=(BYTE{V:puffer$+20}*256+BYTE{V:puffer$+19})/secs|/sides|
    DEC sides|
    FOR i|=0 TO tracks|-1
      FOR i1|=0 TO sides|
        ' Sektoren lesen
        ret%=XBIOS(8,L:V:puffer$,L:0,drive|,1,i|,i1|,secs|)
        IF ret%=0
          ' Fastload formatieren
          ret%=XBIOS(10,L:V:puffer2$,L:0,drive|,secs|,i|,i1|,11,L:&H87654321,&HE5E5)
        ENDIF
        ' Sektoren wieder schreiben
        ret%=XBIOS(9,L:V:puffer$,L:0,drive|,1,i|,i1|,secs|)
      NEXT i1|
    NEXT i|
  ELSE
    ALERT 1,"  Disk kann nicht| konvertiert werden!",1," OK ",dummy|
  ENDIF
ENDIF
