1 ' ****************************************
2 ' *           Bezier - Kurven            *
3 ' *              Listing 1               *
4 ' *       Autor : Kai Hendrik Komp       *
5 ' ****************************************
6 INPUT "Anzahl der FÅhrungspunkte: ";N:N=N-1
7 PRINT "Mit Mauszeiger Punkt markieren und"
8 PRINT "linke Maustaste drÅcken !"
9 DIM Px(N),Py(N),Qx(N),Qy(N),Rx(N),Ry(N)
10 '
11 MOUSEON
12 FOR I%=0 TO N
13  REPEAT
14   Px(I%)= MOUSEX
15   Py(I%)= MOUSEY
16  UNTIL MOUSEBUT =1
17  WAIT .75
18  MOUSEOFF
19  PCIRCLE Px(I%),Py(I%),3
20  IF I%<>0 THEN
21    DRAW Px(I%-1),Py(I%-1) TO Px(I%),Py(I%)
22  ENDIF
23  MOUSEON
24 NEXT I%
25 '
26 ' bestimmt Anzahl der zu berechnenden Pkte
27 Delta!=.01
28 '
29 Xold=Px(0):Yold=Py(0)
30 '
31 T=-Delta!
32 WHILE T<1
33  T=T+Delta!
34  M=N
35  FOR I%=0 TO M
36   Qx(I%)=Px(I%)
37   Qy(I%)=Py(I%)
38  NEXT I%
39  WHILE M>0
40   FOR J%=0 TO M-1
41    Rx(J%)=Qx(J%)+T*(Qx(J%+1)-Qx(J%))
42    Ry(J%)=Qy(J%)+T*(Qy(J%+1)-Qy(J%))
43   NEXT J%
44   M=M-1
45   FOR J%=0 TO M
46    Qx(J%)=Rx(J%)
47    Qy(J%)=Ry(J%)
48   NEXT J%
49  WEND
50  DRAW Xold,Yold TO Qx(0),Qy(0)
51  Xold=Qx(0):Yold=Qy(0)
52 WEND


