' *** Drucker-Treiber erzeugen ***
'
'  Ω 1989 Martin Fangmeyer
'         Wilmeresch 60
'         4430 Steinfurt 1
'
FILESELECT "\*.PRN","DRUCKER.PRN",file$      ! Datei auswÑhlen
'
IF file$<>""
  OPEN "O",#1,file$                          ! Zum Schreiben îffnen
  '
  PRINT #1,"Treiber";                        ! Kennung fÅr Treiber schreiben
  '
  PRINT "Name des Treibers (Druckertype...)  : ";
  FORM INPUT 20,drucker_type_$               ! Name des Druckers abfragen...
  PRINT #1,LEFT$(drucker_type_$+SPACE$(20),20); ! ...und in die Datei schreiben
  '
  REPEAT
    PRINT
    PRINT "Zu ersetzendes Zeichen [RETURN]=Ende: ";
    FORM INPUT 1,char$                       ! Umlaut einlesen
    ascii%=ASC(char$)
    IF ascii%>0                              ! Schon fertig ?
      PRINT                                  ! Nein
      umlaute_$=umlaute_$+char$              ! Umlaut an die Liste anhÑngen
      PRINT "Ersetzen des Zeichens '";char$;"' durch die folgenden,"
      PRINT "Codes (Werte <0=ENDE):"         ! ...ein wenig Text auf den Schirm
      CLR a%,code_neu$                       ! Variablen lîschen
      REPEAT
        PRINT USING "##",a%+1;
        INPUT ". Code: ",code%               ! code% abfragen
        IF code%=>0                          ! code% ok (=>0) ?
          code_neu$=code_neu$+CHR$(code%)    ! Ja, an die Liste anhÑngen
          INC a%
        ENDIF
      UNTIL a%=>10 OR code%<0                ! Max. 10 Codes einlesen
      IF a%>0                                ! War was da ?
        ' zeichen_$ auf gÅltiges Format bringen...
        zeichen_$=zeichen_$+CHR$(a%)+LEFT$(code_neu$+STRING$(10,0),10)
      ELSE                                   ! Keine Codes eingegeben
        umlaute_$=LEFT$(umlaute_$,LEN(umlaute_$)-1) ! => umlaute_$ kÅrzen
      ENDIF
    ENDIF
  UNTIL ascii%=0                             ! [RETURN] fÅr Ende gedrÅckt ?
  '
  OUT #1,LEN(umlaute_$)                      ! Anzahl der umlaute_$ schreiben...
  PRINT #1,umlaute_$;                        ! ...und die Umlaute selbst
  '
  PRINT #1,zeichen_$;                        ! ...und die Anpassungen...
  '
  CLOSE #1                                   ! Ende der Unterhaltung
ENDIF
