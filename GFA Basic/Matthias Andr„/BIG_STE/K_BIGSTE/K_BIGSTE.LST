' ****************************************************
' *           BigSTE-Konfigurationsprogramm    12/90 *
' *           Version 1.02        Matthias Andr„     *
' *           (c) 1991 MAXON Computer                *
' ****************************************************
'
Bootdev%=Dpeek(&H446)+65                   ! Bootlaufwerk
Bste$=Chr$(Bootdev%)+":\AUTO\BIG_STE.PRG"  ! Pfad BigSTE
Desktopinf$=Chr$(Bootdev%)+":\DESKTOP.INF" ! Pfad DESKTOP.INF
'
If Dpeek(Lpeek(Gb+4))<>0 Or (Bios(11,-1) And &H10)
  ' Programm normal gestartet,
  ' oder bei Systemstart aus Auto-Ordner mit Caps-Lock aktiv ?
  Void Bios(11,0)               ! Caps-Lock desaktivieren
  Print
  If Exist(Bste$) Then          ! 'BIG_STE.PRG' suchen.
    Open "U",#1,Bste$
    Seek #1,&H1C+2
    If Input$(4,#1)="BSTE" Then ! Programmkennung korrekt ?
      @Konfiguration            ! Dann konfigurieren.
    Else
      Print "'BIG_STE.PRG' ist nicht BigSTE ..."
      Fehler!=True              ! Programm unbekannt.
    Endif
    Close #1
  Else
    Print "'BIG_STE.PRG' nicht gefunden ..."
    Fehler!=True                ! BigSTE nicht im Auto-Ordner.
  Endif
  '
  Print
  If Fehler! Then
    Print "Konfiguration nicht m”glich !!!"
  Else
    Print "BigSTE konfiguriert ..."
    @Patch_desktop_inf   ! Aufl”sung in "DESKTOP.INF" anpassen
  Endif
  Print
Endif
'
Pause 20
Edit
'
Procedure Konfiguration
  ' Einlesen der alten BigSTE-Parameter, Ver„ndern der Werte
  ' durch Benutzer und neues Setzen der BigSTE-Parameter
  Local A$,B$,A%
  Repeat
    Cls
    Print '''''''Chr$(27);"p BigSTE - Konfiguration ";Chr$(27);"q"
    Print "             Version 1.02"
    Print "          von Matthias Andr„"
    '
    Seek #1,&H1C+6
    '
    A$="j"
    Print At(1,5);"BigSTE Installation (j,n) ?: ";
    Form Input 1 As A$
    A$=Upper$(A$)
    A%=Cvi(Input$(2,#1))
    Relseek #1,-2
    If A$="J" Then
      Print #1,Mki$(A% And 1);
      Relseek #1,-2
      @Werteingabe(7,"Farb-Aufl”sungsmodus [0,1] ?: ",0,1,1,1)
      '
      Print At(1,9);"Niedrige Aufl”sung (in Pixel)"
      @Werteingabe(10,"Horizontal [320..1280] ?: ",320,1280,16,4)
      @Werteingabe(11,"  Vertikal  [200..800] ?: ",200,800,8,3)
      Print At(1,9);"Mittlere Aufl”sung (in Pixel)"
      @Werteingabe(10,"Horizontal [640..2560] ?: ",640,2560,16,4)
      @Werteingabe(11,"  Vertikal  [200..800] ?: ",200,800,8,3)
      Print At(1,9);"Hohe Aufl”sung (in Pixel)    "
      @Werteingabe(10,"Horizontal [640..2560] ?: ",640,2560,16,4)
      @Werteingabe(11,"  Vertikal [400..1600] ?: ",400,1600,16,4)
      '
      Print At(1,13);"Schieberand (niedrige Aufl”sung)"
      @Werteingabe(14,"Horizontal [0..120] ?: ",0,120,1,3)
      @Werteingabe(15,"  Vertikal  [0..75] ?: ",0,75,1,2)
      Print At(1,13);"Schieberand (mittlere Aufl”sung)"
      @Werteingabe(14,"Horizontal [0..240] ?: ",0,240,1,3)
      @Werteingabe(15,"  Vertikal  [0..75] ?: ",0,75,1,2)
      Print At(1,13);"Schieberand (hohe Aufl”sung)    "
      @Werteingabe(14,"Horizontal [0..240] ?: ",0,240,1,3)
      @Werteingabe(15,"  Vertikal [0..150] ?: ",0,150,1,3)
      '
      B$="j"
      Print At(1,17);"Eingaben in Ordnung (j,n): ";
      Form Input 1 As B$
      B$=Upper$(B$)
    Else
      Print #1,Mki$(A% Or 2);
    Endif
  Until A$<>"J" Or B$="J"
Return
'
Procedure Werteingabe(Y%,Text$,Min%,Max%,Schritt%,Stellen%)
  ' Automatisierte Werteingaberoutine
  Local Wert$
  Wert$=Str$(Cvi(Input$(2,#1))+Min%)
  Repeat
    Print At(1,Y%);Chr$(27);"K";Text$;
    Form Input Stellen% As Wert$
    Wert$=Str$((Val(Wert$) Div Schritt%)*Schritt%)
  Until Val(Wert$)>=Min% And Val(Wert$)<=Max%
  Print At(1,Y%);Chr$(27);"K";Text$;Wert$
  Relseek #1,-2
  Print #1,Mki$(Val(Wert$)-Min%);
Return
'
Procedure Patch_desktop_inf
  Local A$,A%,B%,P%
  Print
  If Exist(Desktopinf$)
    If Exist(Bste$) Then
      Open "U",#1,Desktopinf$
      Open "I",#2,Bste$
      Seek #2,&H1C+2
      If Input$(4,#2)="BSTE" Then  ! BigSTE Programmkennung korrekt ?
        A%=Cvi(Input$(2,#2))       ! BigSTE Aufl”sungsstufe lesen.
        If A%<2 Then               ! Soll BigSTE installiert werden ?
          A%=2-A%                  ! Aufl”sung in 'DESKTOP.INF'-Format
          A$=Input$(Lof(#1),#1)
          P%=Instr(A$,"#E ")       ! Aufl”sungsinformation im "DESKTOP.INF"
          If P%>0 Then             ! gefunden ?
            B%=Val("&"+Mid$(A$,P%+7,1))  ! Lesen
            If B%<>A%              ! Aufl”sungen unterschiedlich ?
              Seek #1,P%+6
              Out #1,Asc(Hex$(A%)) ! Aufl”sung in "DESKTOP.INF" schreiben.
              Print "BigSTE-Aufl”sung in 'DESKTOP.INF' angepažt ..."
            Endif
          Endif
        Endif
      Else
        Print "'BIG_STE.PRG' ist nicht BigSTE ..." ! Programm unbekannt.
      Endif
      Close
    Else
      Print "'BIG_STE.PRG' nicht gefunden ..."
    Endif
  Else
    Print "'DESKTOP.INF' nicht gefunden ..."
  Endif
  Print
Return
