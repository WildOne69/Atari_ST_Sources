' Objektrahmen in GFA-BASIC 3.0
' Autor: Dirk Beyer
' (c) MAXON Computer GmbH 1990
'
@vorarbeiten
@rahmen_zeichnen
@rahmen_veraendern
'
PROCEDURE vorarbeiten
  mindestbreite%=24  ! in Pixeln, Mindestmaûe fÅr
  mindesthoehe%=16   ! Objektrahmen
  PRINT AT(1,1);"Abbruch mit rechter Maustaste!"
  DIM rahmen%(5,5)
  x0%=200      !  Anfangskoordinaten des Objektrahmens
  y0%=100      !  festlegen
  x1%=440
  y1%=300
  rahmen%(0,0)=x0%  ! X-Position des Rahmens
  rahmen%(0,1)=y0%  ! Y-Position oben
  rahmen%(0,4)=x1%  ! X-Position rechts
  rahmen%(2,1)=y1%  ! Y-Position unten
  rahmenkoordinaten_zuweisen
RETURN
PROCEDURE rahmen_veraendern
  REPEAT
    REPEAT
      MOUSE mx%,my%,mk%
    UNTIL mk%<>0  ! auf Maustaste warten
    IF mk%=1      ! falls linke Maustaste gedrÅckt
      groesse_aendern!=FALSE
      FOR x%=0 TO 4 STEP 2
        FOR y%=0 TO 2
          IF mx%>=rahmen%(y%,x%)-4 AND mx%<=rahmen%(y%,x%)+4
            IF my%>=rahmen%(y%,x%+1)-4 AND my%<=rahmen%(y%,x%+1)+4
              wahl_x%=x%   ! Wahl der
              wahl_y%=y%
              SPUT screen$
              groesse_aendern!=TRUE
            ENDIF
          ENDIF
        NEXT y%
      NEXT x%
      IF groesse_aendern!=TRUE  ! Falls
        rahmengroesse_aendern   ! angeklickt,
      ELSE                      ! sonst ...
        x0%=rahmen%(0,0)
        y0%=rahmen%(0,1)
        breite%=rahmen%(0,4)-x0%
        hoehe%=rahmen%(2,5)-y0%
        IF mx%>x0% AND mx%<x0%+breite%  ! falls
          IF my%>y0% AND my%<y0%+hoehe%
            DEFMOUSE 4    ! Mauspfeil wird Hand
            SPUT screen$
            GRAPHMODE 3
            BOX x0%,y0%,x0%+breite%,y0%+hoehe%
            no_mouse
            mx1%=mx%   ! Mauskoordinaten merken
            my1%=my%
            REPEAT
              MOUSE mx%,my%,mk%
              IF mx%<>mx1% OR my%<>my1%
                BOX x0%,y0%,x0%+breite%,y0%+hoehe%  !alte Bbox
                x0%=MAX(0,MIN(x0%+mx%-mx1%,639-breite%))
                y0%=MAX(0,MIN(y0%+my%-my1%,399-hoehe%))  ! und
                BOX x0%,y0%,x0%+breite%,y0%+hoehe% ! neue
                mx1%=x0%+breite%/2
                my1%=y0%+hoehe%/2
              ENDIF
            UNTIL mk%<>0
            BOX x0%,y0%,x0%+breite%,y0%+hoehe%
            rahmen%(0,0)=x0% ! Rahmenkoordinaten
            rahmen%(0,1)=y0%
            rahmen%(0,4)=x0%+breite%
            rahmen%(2,1)=y0%+hoehe%
            @rahmenkoordinaten_zuweisen
            GRAPHMODE 1
            rahmen_zeichnen  ! Rahmen mit Objekt
            no_mouse
            DEFMOUSE 0   ! wieder Pfeil als Maus
          ENDIF
        ENDIF
      ENDIF
    ENDIF
  UNTIL mk%=2  ! Ende nach DrÅcken der rechten
  EDIT
RETURN
PROCEDURE no_mouse
  REPEAT
  UNTIL MOUSEK=0   ! warten, bis kein Mausklick
RETURN
PROCEDURE rahmengroesse_aendern
  DEFMOUSE 7       ! Mauspfeil wird Kreuz
  GRAPHMODE 3
  rahmen_zeichnen
  mx%=rahmen%(wahl_y%,wahl_x%)   ! mx%,my% wird
  my%=rahmen%(wahl_y%,wahl_x%+1) ! gewÑhlten
  '                               box zugewiesen
  SETMOUSE mx%,my%        ! Mauspfeil in die
  '                  Grîûeneinstellungsbox setzen
  mx1%=mx%
  my1%=my%
  no_mouse
  REPEAT
    MOUSE mx%,my%,mk%
    IF mx%<>mx1% OR my%<>my1%
      mx1%=mx%
      my1%=my%
      alt_x0%=rahmen%(0,0)        ! bisherige
      alt_y0%=rahmen%(0,1)        ! an "alt"-
      alt_x1%=rahmen%(2,4)
      alt_y1%=rahmen%(2,5)
      IF wahl_x%=0 OR wahl_x%=4
        rahmen%(0,wahl_x%)=mx%
        rahmen%(1,wahl_x%)=mx%
        rahmen%(2,wahl_x%)=mx%
      ENDIF
      rahmen%(wahl_y%,1)=my%
      rahmen%(wahl_y%,3)=my%
      rahmen%(wahl_y%,5)=my%
      rahmenkoordinaten_zuweisen
      neu_x0%=rahmen%(0,0)
      neu_y0%=rahmen%(0,1)
      neu_x1%=rahmen%(2,4)
      neu_y1%=rahmen%(2,1)
      IF neu_x1%-neu_x0%<mindestbreite%
        IF neu_x1%<>alt_x1%
          rahmen%(0,4)=neu_x0%+mindestbreite%
          rahmen%(1,4)=rahmen%(0,4)
          rahmen%(2,4)=rahmen%(0,4)
        ELSE
          rahmen%(0,0)=rahmen%(0,4)-mindestbreite%
          rahmen%(1,0)=rahmen%(0,0)
          rahmen%(2,0)=rahmen%(0,0)
        ENDIF
        rahmenkoordinaten_zuweisen
      ENDIF
      IF neu_y1%-neu_y0%<mindesthoehe%
        IF neu_y1%<>alt_y1%
          rahmen%(2,1)=rahmen%(0,1)+mindesthoehe%
          rahmen%(2,3)=rahmen%(0,5)
          rahmen%(2,5)=rahmen%(0,5)
        ELSE
          rahmen%(0,1)=rahmen%(2,1)-mindesthoehe%
          rahmen%(0,3)=rahmen%(0,1)
          rahmen%(0,5)=rahmen%(0,1)
        ENDIF
        rahmenkoordinaten_zuweisen
      ENDIF
      neu_x0%=rahmen%(0,0)
      neu_y0%=rahmen%(0,1)
      neu_x1%=rahmen%(2,4)
      neu_y1%=rahmen%(2,1)
      ' nur dann alten Rahmen lîschen und neuen
      ' neue Koordinaten sich von bisherigen
      IF neu_x0%<>alt_x0% OR neu_x1%<>alt_x1%
        BOX alt_x0%,alt_y0%,alt_x1%,alt_y1%
        rahmen_zeichnen
      ELSE
        IF neu_y0%<>alt_y0% OR neu_y1%<>alt_y1%
          BOX alt_x0%,alt_y0%,alt_x1%,alt_y1%
          rahmen_zeichnen
        ENDIF
      ENDIF
    ENDIF
  UNTIL mk%<>0
  rahmen_zeichnen
  groesse_aendern!=FALSE
  GRAPHMODE 1
  SGET screen$
  rahmen_zeichnen
  REPEAT
  UNTIL MOUSEK=0
  DEFMOUSE 0
RETURN
PROCEDURE rahmen_zeichnen  ! Objektrahmen+Objekt
  IF NOT groesse_aendern!
    SGET screen$  ! Hintergrund retten
  ENDIF
  ' Rahmenbox zeichnen...
  BOX rahmen%(0,0),rahmen%(0,1),rahmen%(2,4),rahmen%(2,5)
  x_links%=rahmen%(0,0)
  x_mitte%=rahmen%(0,2)
  x_rechts%=rahmen%(0,4)
  y_oben%=rahmen%(0,1)
  y_mitte%=rahmen%(1,1)
  y_unten%=rahmen%(2,1)
  IF NOT groesse_aendern!
    ' Grîûreneinstellungsboxen zeichnen...
    PBOX x_links%-2,y_oben%-2,x_links%+2,y_oben%+2
    PBOX x_links%-2,y_mitte%-2,x_links%+2,y_mitte%+2
    PBOX x_links%-2,y_unten%-2,x_links%+2,y_unten%+2
    PBOX x_rechts%-2,y_oben%-2,x_rechts%+2,y_oben%+2
    PBOX x_rechts%-2,y_mitte%-2,x_rechts%+2,y_mitte%+2
    PBOX x_rechts%-2,y_unten%-2,x_rechts%+2,y_unten%+2
    PBOX x_mitte%-2,y_oben%-2,x_mitte%+2,y_oben%+2
    PBOX x_mitte%-2,y_unten%-2,x_mitte%+2,y_unten%+2
    ' Hier kînnen Sie Ihr Rahmenobjekt einfÅgen:
    ' Rahmenobjekt zeichnen...
    ELLIPSE x_mitte%,y_mitte%,x_rechts%-x_mitte%,y_unten%-y_mitte%
  ENDIF
RETURN
PROCEDURE rahmenkoordinaten_zuweisen
  ' rahmen%() enthÑlt Koordinaten der
  rahmen%(0,2)=(rahmen%(0,0)+rahmen%(0,4))/2
  rahmen%(0,3)=rahmen%(0,1)
  rahmen%(0,5)=rahmen%(0,1)
  rahmen%(1,0)=rahmen%(0,0)
  rahmen%(1,1)=(rahmen%(0,1)+rahmen%(2,1))/2
  rahmen%(1,4)=rahmen%(0,4)
  rahmen%(1,5)=rahmen%(1,1)
  rahmen%(2,0)=rahmen%(0,0)
  rahmen%(2,2)=rahmen%(0,2)
  rahmen%(2,3)=rahmen%(2,1)
  rahmen%(2,4)=rahmen%(0,4)
  rahmen%(2,5)=rahmen%(2,1)
RETURN
