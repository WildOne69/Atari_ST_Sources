' MAKE.DRK.GFA
' verwandelt .N9-, .N24- und .LAS-Dateien zurÅck in .DRK-Dateien
' (C) Heinrich Emmerl 03/91
'
DIM sp%(5000),p$(500)
speich%=VARPTR(sp%(0))
CLS
PRINT
PRINT "  RÅckverwandeln von TEMPUS WORD-Treibern (.N9 .N24 .LAS) in Textfiles (.DRK)"
PRINT SPC(19);"Bitte zuerst die Treiber-Datei auswÑhlen!"
PRINT
FILESELECT #"Treiber-Datei auswÑhlen","","",datei$
IF datei$<>"" AND RIGHT$(datei$,1)<>"\"
  pfad$=MID$(datei$,1,RINSTR(datei$,"\"))
  OPEN "I",#1,datei$
  l%=LOF(#1)
  BLOAD datei$,speich%
  CLOSE #1
ELSE
  END
ENDIF
'
PRINT "öbersetze Namen"
start%=speich%+1932
p$(0)=SPACE$(12)
BMOVE start%,VARPTR(p$(0)),12
p$(0)=LEFT$("TWDT"+p$(0)+SPACE$(20),20)+"*"
DO
  null%=INSTR(p$(0),CHR$(0))
  IF null%
    MID$(p$(0),null%,1)=" "
  ENDIF
LOOP UNTIL null%=0
' PRINT p$(0)
ADD start%,13
p$(1)=STR$(PEEK(start%))
' PRINT p$(1)
'
PRINT "öbersetze Codes"
i%=1
DO
  fertig!=FALSE
  INC i%
  SELECT i%
  CASE 4
    wort(5)
  CASE 94
    INC start%
    IF PEEK(start%)<>255
      p$(i%)=STR$(256*PEEK(start%)+PEEK(start%+1))+" "
      INC start%
      fertig!=FALSE
    ELSE
      fertig!=TRUE
    ENDIF
  CASE 96
    wort(2)
  CASE 97,98
    wort_byte
  ENDSELECT
  IF fertig!=FALSE
    DO
      INC start%
      p|=PEEK(start%)
      IF p|<>255
        p$(i%)=p$(i%)+STR$(p|)+","
      ENDIF
    LOOP UNTIL p|=255
  ENDIF
  IF RIGHT$(p$(i%),1)=","
    MID$(p$(i%),LEN(p$(i%)),1)=" "
    trimmen
  ENDIF
  ' PRINT p$(i%)
LOOP UNTIL i%=309
'
PRINT "öbersetze Prop-Tabellen"
DO
  INC i%
  y%=0
  DO
    INC start%
    p|=PEEK(start%)
    IF p|<>255
      INC y%
      IF y%<3
        p$(i%)=p$(i%)+STR$(p|)+","
      ELSE
        p$(i%)=p$(i%)+STR$(p|)+" "
        y%=0
      ENDIF
    ENDIF
  LOOP UNTIL p|=255 OR start%>=speich%+l%
  trimmen
  ' PRINT p$(i%)
LOOP UNTIL i%=481
'
CLS
PRINT
PRINT
PRINT SPC(15);"Bitte nun die zu erzeugende Text-Datei auswÑhlen!"
FILESELECT #"Text-Datei speichern (.DRK)",pfad$+"*.DRK","",datei$
IF datei$<>"" AND RIGHT$(datei$,1)<>"\"
  OPEN "O",#1,datei$
  STORE #1,p$(),482
  CLOSE #1
ENDIF
ALERT 2,"|Noch eine Åbersetzen?",1,"NEIN| JA ",dum%
IF dum%=1
  END
ELSE
  RUN
ENDIF
'
PROCEDURE trimmen
  p$(i%)=TRIM$(p$(i%))
RETURN
'
PROCEDURE wort(words|)
  y%=0
  INC start%
  IF PEEK(start%)<>255
    DO
      p$(i%)=p$(i%)+STR$(256*PEEK(start%)+PEEK(start%+1))+" "
      ADD start%,2
      INC y%
    LOOP UNTIL y%=words|
    trimmen
  ENDIF
  fertig!=TRUE
RETURN
'
PROCEDURE wort_byte
  INC start%
  IF PEEK(start%)<>255
    p$(i%)=p$(i%)+STR$(256*PEEK(start%)+PEEK(start%+1))+" "
    p$(i%)=p$(i%)+STR$(PEEK(start%+2))+","
    ADD start%,2
    trimmen
    fertig!=FALSE
  ELSE
    fertig!=TRUE
  ENDIF
RETURN
