' Patch fÅr RCS 1.4
' (c) Jan Willamowius, 1991
' Der Puffer fÅr Images wird vergrîûert
'
OPEN "U",#1,"RCS.PRG"  ! RCS zum lesen/schreiben îffnen
'
' LÑnge des BSS-Segment holen
'
SEEK #1,&HA
bss$=INPUT$(4,#1)
bss%=LPEEK(VARPTR(bss$))
IF bss%>55000 THEN
  PRINT "Falsches oder schon gepatchtes RCS!"
  END
ENDIF
'
' BSS-Segment um 32.000 Bytes verlÑngern
'
bss%=bss%+32000
'
' neue LÑnge byteweise schreiben
'
SEEK #1,&HA
OUT #1,bss% DIV &H1000000
bss%=bss% MOD &H1000000
OUT #1,bss% DIV &H10000
bss%=bss% MOD &H10000
OUT #1,bss% DIV &H100
bss%=bss% MOD &H100
OUT #1,bss%
'
' an 7 Stellen die neue Adresse des Puffers einfÅgen
'
GOSUB patch(&H77A)
GOSUB patch(&H7AE)
GOSUB patch(&H820)
GOSUB patch(&H47C4)
GOSUB patch(&H47DE)
GOSUB patch(&H51C2)
GOSUB patch(&H542C)
CLOSE #1             ! RCS schlieûen
END                  ! That's all Charlie Brown!
'
' alte Adresse durch neue erstetzen
'
PROCEDURE patch(ort%)
  SEEK #1,ort%
  test$=INPUT$(4,#1)
  IF LPEEK(VARPTR(test$))=&H1A586 THEN  ! alter Puffer ist bei $1A586
    SEEK #1,ort%
    OUT #1,&H0                          ! neue Adresse ist $1D19A
    OUT #1,&H1
    OUT #1,&HD1
    OUT #1,&H9A
  ELSE
    PRINT "Falsches oder schon gepatchtes RCS!"
    END
  ENDIF
RETURN

