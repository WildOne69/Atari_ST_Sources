(c) 1991 MAXON Computer

PRINT "Zeicheneingabe mit der Maus"
DEFWRD "a-z"
init
DO
  l=0
  ARRAYFILL linien(),0
  ARRAYFILL maus|(),0
  eingabe
  linien
  erkennung
LOOP
PROCEDURE eingabe
  p=0
  REPEAT
    IF UPPER$(INKEY$)="C"
      CLS
    ENDIF
    MOUSE sx,sy,k
  UNTIL k=1
  PLOT sx,sy
  xmax=sx
  ymax=sy
  xmin=sx
  ymin=sy
  ax=sx
  ay=sy
  REPEAT
    MOUSE x,y,k
    IF k=3
      EDIT
    ENDIF
    DRAW  TO x,y
    xmax=MAX(xmax,x)
    ymax=MAX(ymax,y)
    xmin=MIN(xmin,x)
    ymin=MIN(ymin,y)
    IF ax-x<>0 OR ay-y<>0
      maus|(p,0)=BYTE(x-ax)
      maus|(p,1)=BYTE(y-ay)
      ax=x
      ay=y
      INC p
    ENDIF
    IF p>max
      ALERT 3,"Soviele Mausbewegungen|werden Sie ja wohl|nicht brauchen!",1,"Stimmt",back
      RUN
    ENDIF
  UNTIL k=0
RETURN
PROCEDURE linien
  LOCAL p1,hypothese,h1
  eps=MAX((MAX(xmax-xmin,ymax-ymin)/10)^2,16)
  eps1#=0.45
  hypothese=-1
  x1=0
  y1=0
  FOR p1=0 TO p
    dx=dx+WORD(maus|(p1,0)+SHL(BTST(maus|(p1,0),7),8))
    dy=dy+WORD(maus|(p1,1)+SHL(BTST(maus|(p1,1),7),8))
    IF dx=0
      h1=2-4*(dy>0)
    ELSE IF ABS(dy/dx)<eps1#
      h1=-4*(dx<0)
    ELSE IF ABS(dx/dy)<eps1#
      h1=2-4*(dy>0)
    ELSE
      h1=1-4*(dy>0)-2*(SGN(dx)=SGN(dy))
    ENDIF
    IF h1<>hypothese
      IF (dx*dx+dy*dy)>eps
        ' Neue Hypothese!
        linien(l,0)=hypothese
        linien(l,1)=SQR(x1*x1+y1*y1)
        hypothese=h1
        x1=dx
        y1=dy
        dx=0
        dy=0
        INC l
      ENDIF
    ELSE
      ' Immer noch alte Hypothese
      ADD x1,dx
      ADD y1,dy
      dx=0
      dy=0
    ENDIF
  NEXT p1
  linien(l,0)=hypothese
  linien(l,1)=SQR(x1*x1+y1*y1)
RETURN
PROCEDURE erkennung
  ok!=TRUE
  p=0  !p ist ein Pointer im Feld linien()
  k1=letzter
  DO WHILE ok!
    INC p !nÑchste Linie untersuchen
    k=k1
    a=1
    WHILE a<=4 AND baum|(k,a)<>255
      h=baum|(baum|(k,a),0)
      b=0 !Der Offset zu p bei der Suche
      plausibel!=FALSE
      ' taucht eine Linie=h auf, so wird plausibel! auf TRUE gesetzt
      DO
        IF linien(p+b,0)=h 
          plausibel!=TRUE
        ELSE IF linien(p+b,1)<=2*SQR(eps) AND (linien(p+b,0)=AND(h+1,7) OR linien(p+b,0)=AND(h-1,7))
          IF plausibel!
            ' Es war schon eine Linie dieser Art da?
            b1=1
            flag!=FALSE
            DO WHILE b1<=4 AND baum|(baum|(k,a),b1)<>255
              IF linien(p+b,0)=baum|(baum|(baum|(k,a),b1),0)
                flag!=TRUE
                EXIT IF TRUE
              ENDIF
              INC b1
            LOOP
            EXIT IF flag!
          ENDIF
        ELSE
          EXIT IF TRUE
        ENDIF
        INC b
      LOOP UNTIL p+b>l
      IF plausibel!
        k1=baum|(k,a)
        p=p+b-1
        EXIT IF TRUE
      ENDIF
      INC a
    WEND
    IF k1=k AND p<=l
      ' Kein Nachfolger und noch Linien
      ok!=FALSE
    ENDIF
  LOOP UNTIL l<p
  IF ok! AND baum|(k,5)<>255
    ' Dann ist baum|(k,5) die erkannte Note
    PRINT namen$(baum|(k,5))
  ELSE
    PRINT "Nicht erkannt!"
  ENDIF
  PAUSE 10
RETURN
PROCEDURE init
  ' Hier wird vor allem der Richtungsbaum
  ' zusammengebastelt.
  LET letzter=40
  DIM baum|(letzter,5),temp|(150)
  ARRAYFILL baum|(),255
  RESTORE linien
  a=0
  DO
    READ t$
    EXIT IF t$="***"
    temp|(a)=VAL(t$)
    INC a
  LOOP
  t_p=1
  p=letzter
  freier_platz=0
  nr=temp|(0)
  REPEAT
    b=1
    WHILE b<=4
      IF baum|(p,b)=255
        ' Das heiût, es wurde kein Nachfolger 
        ' mit der richtigen Richtung gefunden
        baum|(p,b)=freier_platz
        p=freier_platz
        baum|(p,0)=temp|(t_p)
        INC freier_platz
        EXIT IF TRUE
      ELSE IF baum|(baum|(p,b),0)=temp|(t_p)
        ' Das heiût, der Nachfolger des 
        ' jetzigen Platzes stimmt in der 
        ' Richtung. Dann springe dahin.
        p=baum|(p,b)
        EXIT IF TRUE
      ENDIF
      INC b
    WEND
    INC t_p
    IF temp|(t_p)=255
      ' Eine Endemarke
      ' Dann trage die nr ein
      baum|(p,5)=nr
      INC t_p
      nr=temp|(t_p)
      INC t_p
      p=letzter
    ENDIF
  UNTIL t_p>=a
  linien:
  DATA 0,4,0,255,1,4,0,2,255,2,0,2,255,3,0,2,7,255,4,0,2,7,4,7,255
  DATA 5,0,2,7,4,7,4,7,255,6,0,2,7,4,7,4,7,4,7,255,7,0,255,8,4,255
  DATA 9,7,5,7,255,10,0,5,255,11,0,5,0,5,255,12,0,5,0,5,0,5,255
  DATA 13,0,5,0,5,0,5,0,5,255,14,6,1,6,255,15,6,3,0,255,16,6,0,255
  DATA 17,0,7,6,5,4,3,2,1,6,255,18,0,7,6,5,255,19,6,255,***
  ERASE temp|()
  max=1000
  DIM maus|(max,1),linien(max/10,1),namen$(19)
  RESTORE n
  FOR a=0 TO 19
    READ namen$(a)
  NEXT a
  n:
  DATA ganze Note,halbe Note,Viertelnote,Achtelnote,Sechzehntelnote
  DATA 32telnote,64telnote,ganze Pause,halbe Pause,Viertelpause,Achtelpause
  DATA 16telpause,32telpause,64telpause,Auflîsungszeichen,Kreuz,Be
  DATA ViolinschlÅssel,BaûschlÅssel,Taktstrich
RETURN



