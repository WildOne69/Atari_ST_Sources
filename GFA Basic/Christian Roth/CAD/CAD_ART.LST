' ***********************************************
' *                                             *
' * PROGRAMM: CAD a'la Carte                    *
' * SPRACHE:  GFA-BASIC V3.X                    *
' * AUTOREN:  Christian Roth und Matthias Brust *
' * (c) 1992 MAXON Computer                     *
' ***********************************************
'
DIM koords&(5),w&(3) !Felder dimensionieren
HIDEM                !Maus weg
INPUT "Bitte Rasterabstand eingeben (>5): ",st&
CLS
raster               !Raster zeichnen
kreuz(2)             !3 Punkte holen
kreis_3              !Kreis konstruieren
GEMSYS 20            !auf Taste warten
CLS                  !Bildschirm lîschen
raster               !neues Raster zeichnen
kreuz(1)             !2 Punkte holen
kreis_ausschnitt     !Kreisausschnitt konstr.
GEMSYS 20            !auf Taste warten
SHOWM                !Maus wieder herholen
PROCEDURE kreis_3 !Kreis durch 3 Punkte konstr.
  LOCAL x1&,x2&,x3&,y1&,y2&,y3&,xm1,ym1,xm2,ym2
  LOCAL diskr,l,mx&,my&,r&,h1,h2
  x1&=koords&(0)  !Koordinaten der
  y1&=koords&(1)  !drei Punkte holen
  x2&=koords&(2)
  y2&=koords&(3)
  x3&=koords&(4)
  y3&=koords&(5)
  xm1=0.5*SUB(x2&,x1&) !Vektor v
  ym1=0.5*SUB(y2&,y1&) !berechnen
  xm2=0.5*SUB(x3&,x1&)
  ym2=0.5*SUB(y3&,y1&)
  diskr=xm2*ym1-xm1*ym2 !Diskriminante
  IF diskr<>0 THEN
    h1=ym1-ym2
    h2=xm1-xm2
    l=(ym1*h1+xm1*h2)/diskr !LÑnge von m
    mx&=xm2+x1&-l*ym2+1 !Kreismittelpunkt
    my&=ym2+y1&+l*xm2+1 !ermitteln
    r&=SQR(SUB(mx&,x1&)^2+SUB(my&,y1&)^2)+1
    CIRCLE mx&,my&,r& !Radius ber.,Kr. zeichnen
  ENDIF
RETURN
PROCEDURE kreis_ausschnitt !Ausschnitt konstr.
  LOCAL x&,y&,x1&,y1&,r&
  LOCAL i&,xv,yv,w1,w2
  x&=koords&(0)  !Koordinaten holen
  y&=koords&(1)  !Radius berechnen
  r&=SQR(SUB(koords&(2),koords&(0))^2+SUB(koords&(3),koords&(1))^2)
  GRAPHMODE 3    !neuer Grafikmodus fÅr Linie
  CIRCLE x&,y&,r& !ganzen Kreis zeichnen
  FOR i&=0 TO 1  !2 mal Linie darstellen
    x1&=x&       !Anfangspunkt = Mittelpunkt
    y1&=y&
    REPEAT
      DRAW x1&,y1& TO x&,y&  !Linie lîschen
      MOUSE x1&,y1&,k
      DRAW x1&,y1& TO x&,y&  !wieder zeichnen
    UNTIL k=1    !Bis linke Taste gedrÅckt
    DRAW x1&,y1& TO x&,y& !Linie lîschen
    w&(i&*2)=x1&    !Koodinaten
    w&(i&*2+1)=y1&  !festhalten
    REPEAT          !Warten, bis linke
    UNTIL MOUSEK=0  !Maustaste losgelassen
  NEXT i&
  xv=1.0E-12+SUB(w&(0),x&) !Vektor vom Mittelp.
  yv=1.0E-12+SUB(w&(1),y&) !bis Linienendp. ber.
  w1=DEG(ATN(yv/xv))*10*SGN(yv/xv) !Winkel ber.
  IF xv<0 THEN
    w1=1800-w1  !in richtigen Bereich bringen
  ENDIF         !von 0-3600
  MUL w1,SGN(-yv)
  ADD w1,SUB(1,SGN(w1))*ABS(SGN(INT(w1)))*1800
  xv=1.0E-12+SUB(w&(2),x&) !folgendes entspr.
  yv=1.0E-12+SUB(w&(3),y&) !obigen Zeilen
  w2=DEG(ATN(yv/xv))*10*SGN(yv/xv)
  IF xv<0 THEN
    w2=1800-w2
  ENDIF
  MUL w2,SGN(-yv)
  ADD w2,SUB(1,SGN(w2))*ABS(SGN(INT(w2)))*1800
  CIRCLE x&,y&,r& !ganzen Kreis lîschen
  GRAPHMODE 1
  DEFLINE 1,,,    !normaler Linienstil
  CIRCLE x&,y&,r&,w1,w2 !Kreisausschnitt zeichn.
RETURN
PROCEDURE kreuz(anz)    !Fadenkreuz darstellen
  LOCAL x&,y&,x_old&,y_old&,i&
  GRAPHMODE 3           !Neuer Grafikmodus
  DEFLINE -21845,1,,    !Neuer Linienstil
  x_old&=INT(MOUSEX/st&+0.5)*st&
  y_old&=INT(MOUSEY/st&+0.5)*st&
  FOR i&=0 TO anz       !Anzahl d. Klicke
    x&=x_old&
    y&=y_old&
    IF i&=0
      LINE x&,0,x&,399
      LINE 0,y&,639,y&
    ENDIF
    REPEAT
      x_old&=x&
      y_old&=y&
      MOUSE x&,y&,k     !Mauskoord. festhalten
      x&=INT(x&/st&+0.5)*st& !Rasterp. errechnen
      y&=INT(y&/st&+0.5)*st& !   "       "
      IF x&<>x_old& OR y&<>y_old& !Koord. gleich
        LINE x_old&,0,x_old&,399 !altes Kreuz
        LINE 0,y_old&,639,y_old& !lîschen
        LINE x&,0,x&,399   !Neues zeichnen
        LINE 0,y&,639,y&
        SETMOUSE x&,y&
      ENDIF
    UNTIL k=1  !Bis linke Taste gedr.
    DEFLINE 1,0,0
    LINE x&-3,y&-3,x&+3,y&+3 !kleines Kreuz
    LINE x&-3,y&+3,x&+3,y&-3 !zeichnen
    koords&(i&*2)=x&         !Koord.
    koords&(i&*2+1)=y&       !speichern
    DEFLINE -21845,1,0,0
    REPEAT                !Bis Maustaste
    UNTIL MOUSEK=0        !losgelassen
  NEXT i&
  LINE x_old&,0,x_old&,399 !altes Kreuz
  LINE 0,y_old&,639,y_old& !lîschen
  SETMOUSE x&,y&  !Maus auf Rasterp. setzen
  DEFLINE 1,0,0
  GRAPHMODE 1
RETURN
PROCEDURE raster !Raster erzeugen
  LOCAL i&,i1&
  FOR i&=st& TO 640-st& STEP st& !x-Richtung
    FOR i1&=st& TO 400-st& STEP st& !y-Richtung
      PSET i&,i1&,1 !Rasterpunkt setzen
    NEXT i1&
  NEXT i&
RETURN
