* PROCDURE EINGABE

* Dies ist ein Beispiel wie man eine Daten -Eingabe / -énderungs Prozedur,
* aufbauen kann, die die bequeme Eingabe und énderung von Adressen ermîglicht. 
* Dabei kann der Ort mittels "MenÅ" (DISP)ausgwÑhlt werden. 
* Die Eingabemaske funktioniert fÅr zwei DBF-Dateien, von denen die eine
* Privat-Adressen (FJ) die andere Institutionen (FK) enthÑlt mit ent-
* sprechend mehr Feldern (Abteilung, Z.Hd.) .  
* Die Datenbank Orte ist sozusagen "selbst- lernend", da nicht vorhandene 
* Orte eingegeben werden kînnen und dann regelmÑûig zur öbernahme zur 
* VerfÅgung stehen.
ERASE
ASSIGN FRAME(T)
SET GET VIDEO TO 121
SET DELIM OFF
Y.ORT = $(ORTE->ORT,6,9)
X.FID = 'F'+SELECTED()
IF PMENU() = 2 .AND.HMENU() = 1
* Stellt fest ob Neueingabe oder énderung
Y.ORT = '    '
GO &X.FID 0
* bei énderung muû der zu Ñndernde Datensatz gesucht werden, bevor 
* in diese Prozedur gesprungen wird.
ENDIF
* Hier wird die Grîûe der Eingabenaske berechnet.
@  1,1 TO 3,28+VAL($(FIELD(1),AT(',',FIELD(1),2)+1,2))
@  4,1 TO 12,28+VAL($(FIELD(1),AT(',',FIELD(1),2)+1,2))
@  2,02 SAY "EINGABE-MASKE  ADRESSEN  "+ALIAS()
* 1. Eingabemaske fÅr Privatadressen
IF SELECTED() = 'J' 
@  5,05 SAY "Name:"
@  6,02 SAY "Vorname:"
@  5,10 GET NAME PICTURE '!XXXXXXXXXXXXXXXXXXX'
@  6,10 GET VNAME PICTURE '!XXXXXXXXXXXXXX'
ENDIF
* 2. Eingabemaske fÅr Adressen von Institutionen
IF SELECTED() = 'L' 
@  5,02 SAY "Institution: "
@  6,02 SAY "Abteilung: "
@  7,02 SAY "zu HÑnden: "
@  5,16 GET INST PICTURE '!XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX'
@  6,16 GET ABT PICTURE '!XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX'
@  7,16 GET ZHD PICTURE '!XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX'
ENDIF
* Ab hier sind die Felder wieder in beiden Datenbanken gleich.
@ ROW()+2,02 SAY "Straûe:"
Y.TROW = ROW()+1
@ ROW(),10 GET STRA PICTURE '!XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX'
@ ROW()+1,06 SAY "Ort:" 
IF PMENU() = 2 .AND.HMENU() = 1
@ ROW(),10 GET Y.ORT PICTURE '!XLL'
ELSE
* Bei einer énderung wird ein anderes MenÅ ausgegeben, da die Rufnummer ja
* schon bekann ist 
@ ROW(),10 SAY ORTE->ORT 
@ ROW(),40 SAY 'Tel: '+ORTE->VORW+'/'+TEL
@ ROW()+1,10 GET Y.ORT PICTURE "!LLLLLLLL" 
ENDIF
READ
* 'Mal CHecKen, ob Åberhaupt 'was eingegeben wurde..
Y.CHK= $(FIELD(1),1,AT(',',FIELD(1),1)-1)
IF &Y.CHK = '   '
RETURN
ENDIF
SELE FM
* Die Datei ORTE.DBF ist nach Ortsnamen $(ORT,6,LEN(TRIM(ORT))) indiziert.
IF Y.ORT <> ORTE->ORT
FIND Y.ORT
        ASSIGN WINDOW(13,50,22,78)
IF EOF()
* Falls der Ort noch nicht existiert wird eben ein N(euer)ORT eingegeben.
DO R_NORT
ENDIF
* Hier kann jetzt der Ort mit dem Cursor angefahren werden.
DISP NEXT 13 OFF $(ORT,1,26) FOR $(ORT,6,3)=Y.ORT
ERASE
Y.ENTSCH = T
* Man darf sich schon wieder geirrt haben.
@ 1,0 SAY CENTER(ORTE->ORT,28)
@ 2,0 TO 2,28
@ 3,1 SAY 'öbernehmen   Y/N  ' GET Y.ENTSCH PICT 'Y'
READ
IF .NOT. Y.ENTSCH
DO R_NORT
ENDIF
ASSIGN WINDOW(0,0,24,80)
@ 13,49 CLEAR TO 23,79
@ Y.TROW,06 SAY "Ort: "
@ Y.TROW+1,06 SAY "Tel: "
SET SAY  VIDEO TO 121
@ Y.TROW,11 ESAY  TRIM(ORT)
@ ROW()+1,11 ESAY  TRIM(ORTE->VORW)+'/'
SET SAY  VIDEO TO 7
SELE &X.FID
* Jetzt noch die Rufnummer
@ ROW(),COL() GET TEL PICTURE '############' 
READ
SELE &X.FID
REPLACE ORT WITH RECNO(FM)
ENDIF
IF PMENU() = 2 .AND.HMENU() = 1
* Letzte Mîglichkeit zu Aussteigen.
Y.ENTSCH = Y
@ 22,2 to 24,78
@ 23,5 SAY 'DATENSATZ '+ALIAS()+' NR.: '+LTRIM(STR(LASTREC(&X.FID)+1,4,0));
+' ANFöGEN ?'
@ 23,50 SAY '----> ' GET Y.ENTSCH PICT 'Y'
@ 23,58 SAY '<---- ' 
READ
IF Y.ENTSCH
* Jetzt wird der Datensatz angefÅgt.
APPEND RECORD
ENDIF
ENDIF
RETURN
