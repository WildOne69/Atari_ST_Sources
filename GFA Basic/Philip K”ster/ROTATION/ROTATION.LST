' Gedrehte Bildschirmausschnitte in GFA-BASIC 3.0
' (c) 1990 by Philip Kîster
' Am Waldbad 13 - 2122 Bleckede 6
'
DEFFILL 1,3,12             !Ausschnitt zeichnen ...
PBOX 0,0,95,95
'
TEXT 1,14,"  Dies ist"
TEXT 1,28,"  ein GET-"
TEXT 1,42,"Ausschnitt"
'
GET 0,0,95,95,ausschnitt$  !... und speichern
'
DEFFILL 1,2,4  !Demo: HintergrundverknÅpfung
PBOX 0,0,319,199
'
FOR i|=1 TO 2
  TEXT (i|-1)*160,15,"Modus "+STR$(i|)+", Alpha = 0"
  TEXT (i|-1)*160,31,"b = 96, h = 96    "
  '
  put(40+(i|-1)*160,40,-1,-1,ausschnitt$,i|,0)  !Prozedur-Aufruf
NEXT i|
'
TEXT 320,15,"Modus 2, Alpha = 0"  !Demo: GrîûenverÑnderung
TEXT 320,31,"b = 48, h = 96"
'
put(360,40,48,96,ausschnitt$,2,0)
'
TEXT 480,15,"Modus 2, Alpha = 0"
TEXT 480,31,"b = 48, h = 48"
'
put(520,40,48,48,ausschnitt$,2,0)
'
FOR i|=1 TO 4  !Demo: Drehen
  TEXT (i|-1)*160,215,"Modus 2, Alpha = "+STR$(i|*20)
  TEXT (i|-1)*160,231,"b = 96, h = 96"
  '
  put(40+(i|-1)*160,240,-1,-1,ausschnitt$,2,i|*20)
NEXT i|
'
PRINT AT(1,25);"Taste drÅcken.";
~INP(2)
'
END  !Jetzt geht's erst los ...
'
PROCEDURE put(x&,y&,pb&,ph&,ausschnitt$,modus|,alpha&)
  LOCAL dx&,dy&,x0&,y0&,b&,h&,fx,fy,quelle!,ziel!,ergebnis!  !Nicht im Hauptprogramm stîren
  '
  b&=CVI(MID$(ausschnitt$,1,2))+1  !Breite ...
  h&=CVI(MID$(ausschnitt$,3,2))+1  !... und Hîhe des Ausschnitts feststellen
  '
  IF pb&<0  !Keine GrîûenverÑnderung in x-...
    pb&=b&
  ENDIF
  IF ph&<0  !... oder y-Richtung?
    ph&=h&
  ENDIF
  '
  fx=pb&/b&  !Projektionsfaktoren in x-...
  fy=ph&/h& !... und y-Richtung feststellen
  '
  IF alpha& MOD 90  !"Ungerade Drehung": Keine VerÑnderung der Proportionen erlaubt
    fy=fx
  ENDIF
  '
  FOR dy&=0 TO h&-1  !Von oben nach unten
    x0&=x&+dy&*fx*COSQ(90+alpha&)  !Rotationspunkt der aktuellen ...
    y0&=y&+dy&*fy*SINQ(90+alpha&)  !... gedrehten Zeile feststellen
    '
    FOR dx&=0 TO b&-1  !Von links nach rechts
      quelle!=BTST(CVI(MID$(ausschnitt$,7+(dy&*b&/16+dx& DIV 16)*2,2)),15-dx& MOD 16)  !Aktuelles Bit aus String isolieren
      '
      ziel!=POINT(x0&+dx&*fx*COSQ(alpha&),y0&+dx&*fy*SINQ(alpha&))  !Punkt schon gesetzt?
      '
      SELECT modus|  !VerknÅpfungsart feststellen
      CASE 1  !GRAPHMODE 1
        ergebnis!=quelle!
      CASE 2  !GRAPHMODE 2
        ergebnis!=quelle! OR ziel!
      CASE 3  !GRAPHMODE 3
        ergebnis!=quelle! XOR ziel!
      CASE 4  !GRAPHMODE 4
        ergebnis!=(NOT quelle!) OR ziel!
      ENDSELECT
      '
      IF ergebnis!  !Farbe bestimmen
        COLOR 1
      ELSE
        COLOR 0
      ENDIF
      '
      PLOT x0&+dx&*fx*COSQ(alpha&),y0&+dx&*fy*SINQ(alpha&)  !Punkt setzen
    NEXT dx&
  NEXT dy&
RETURN  !War doch gar nicht so viel.
