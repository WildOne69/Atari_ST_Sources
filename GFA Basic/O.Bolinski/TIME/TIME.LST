'
' 'GFA IM HINTERGRUND'  -  zum Beispiel ein TIME - ACCESSORY :
'
'  Written By    O.Bolinski & A.Backes    Januar 1990
'
'  O.Bolinski
'  Postfach 181108
'  5500 TRIER
'
'
$ m50000               ! 50 KBYTE RESERVIEREN ALS ACCESSORY-ARBEITSBEREICH
'
' PARAMETER BLOCK :
uhr_status%=0          ! WENN 1 => UHR BEI SYSTEMSTART AN-, SONST AUSGESCHALTET
DIM a(7)               ! FELD FšR EINZELZIFFERN DER AKTUELLEN SYSTEMZEIT
'
' ACCESSOIRY INSTALLIEREN
ap_id&=APPL_INIT()
me_id&=MENU_REGISTER(ap_id&,"  Time Install ")
'
' HAUPTSCHLEIFE ( WIRD STŽNDIG VOM BETRIEBSSYSTEM DURCHLAUFEN )
DO
  a$=TIME$                                               ! AKTUELLE ZEIT RETTEN
  ' AUF TIMER- ODER ACCESSOIRY-EVENT WARTEN
  a%=EVNT_MULTI(&X110000,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1000)
  IF MENU(1)=40 AND a%=&X10000
    ' DAS TIME-ACCESSOIRY WURDE AUFGERUFEN
    ' BILDSCHIRM RETTEN
    ~WIND_UPDATE(1)
    GET 0,18,639,399,rette$
    ~GRAF_GROWBOX(320,200,0,0,200,80,240,240)
    GOSUB desktop
    DEFTEXT 1,0,0,13
    SHOWM
    ' ACC-MAIN-SCHLEIFE ZUR ABFRAGE DER BUTTONS
    DO
      MOUSE x,y,k                                           ! MAUSPOSITION
      IF x=>218 AND x=<423 AND y=>165 AND y<=205 AND k=1    ! ERMITTELN
        ' ZEIT SETZEN DURCH KLICKEN AUF DEN BUTTON
        GOSUB s.et_time
        a$=TIME$
      ELSE IF x=>218 AND x=<423 AND y=>95 AND y<=150 AND k=1
        ' UHR AN/AUSSCHALTEN DURCH KLICKEN AUF DIE HEADLINE
        ALERT 0,"Bildschirm - Uhr | ",1," An | Aus ",alert_wahl%
        IF alert_wahl%=1                                   ! WENN -AN-  GEWŽHLT
          uhr_status%=1                                    ! DANN uhr_status%=1
        ELSE                                               ! WENN -AUS- GEWŽHLT
          uhr_status%=0                                    ! DANN uhr_status%=0
          PRINT AT(72,1);"         "     ! 9 LEERZEICHEN UM DESKTOP ZU REINIGEN
        ENDIF
      ENDIF
      ' ACCESSOIRY VERLASSEN BEI KLICK AUF O.K.
      EXIT IF x>=374 AND x<=423 AND y>=210 AND y<=305 AND k=1
    LOOP
    ' BILDSCHIRM RESTAURIEREN
    PUT 0,18,rette$
    ~GRAF_SHRINKBOX(320,200,0,0,200,80,240,240)
    rette$=""
    ~WIND_UPDATE(0)
  ENDIF
  IF uhr_status%=1
    IF a%=&X100000
      ' TIMER-EVENT EINGETRETEN ... EINE SEKUNDE WARTEZEIT IST VERSTRICHEN
      ' AUCH UNGERADE SEKUNDEANZAHLEN AUSGEBEN
      i=VAL(RIGHT$(a$,1))
      IF a$=TIME$
        i=i+1
      ELSE
        i=i+2
        IF i=10
          i=0
        ENDIF
      ENDIF
      a$=LEFT$(TIME$,7)+STR$(i)
      ' EINZELZIFFERN IN DIGITALZAHLEN WANDELN & AUSGEBEN
      v=VARPTR(a$)                   ! STARTADRESSE DER VARIABLE IN v
      i=0                            ! v IST ADRESSE DES ERSTEN ZEICHENS VON a$
      DEFTEXT 1,0,0,13
      FOR x=0 TO 2
        TEXT 575+i*8,14,CHR$(BYTE{v+i}-32)
        ' -BYTE{[1. Zeichen + i. Zeichen]}-  AN ADRESSE v WERDEN VON a$ JE EINE
        '  ZIFFER GELESEN UND IN DIGITALZIFFERN GEWANDELT
        INC i                                 ! NŽCHSTE ZIFFER ...
        TEXT 575+i*8,14,CHR$(BYTE{v+i}-32)
        i=i+2                                 ! DOPPELPUNKT šBERSPRINGEN ...
        TEXT 575+(i-1)*8,14,":"               ! UND AUSGEBEN.
      NEXT x
    ENDIF
  ENDIF
LOOP
'
PROCEDURE desktop
  ' ACCESSOIRY ZEICHNEN
  DEFFILL 0
  PBOX 200,80,640-200,400-80
  BOX 200,80,640-200,400-80
  BOX 201,81,640-201,400-81
  BOX 203,83,640-203,400-83
  BOX 213,93,640-213,400-250
  BOX 215,95,640-211,400-248
  BOX 218,165,640-217,205
  BOX 218,210,640-269,255
  BOX 218,260,640-269,305
  DEFTEXT 1,0,0,13
  TEXT 220,120,"   SETTIME - Accessory"
  TEXT 237,190,"Zeit/Datum einstellen"
  TEXT 394,265,"Ok"
  DEFTEXT 1,2,0,13
  TEXT 252,239,DATE$
  TEXT 260,289,TIME$
  DEFTEXT 1,0,0,4
  TEXT 243,135," O.BOLINSKI  &   A.BACKES "
  DEFTEXT 1,0,0,13
RETURN
'
PROCEDURE s.et_time
  ' ZEIT UND DATUM EINSTELLEN
  PRINT AT(30,12);"Zeit  :               ";
  PRINT AT(40,12);
  FORM INPUT 2,a$
  PRINT AT(42,12);":";
  FORM INPUT 2,b$
  PRINT AT(45,12);":";
  FORM INPUT 2,c$
  ' EINGEGEBENE ZEIT ALS SYSTEMZEIT ANMELDEN
  TIME$=a$+":"+b$+":"+c$
  PRINT AT(30,12);"Datum :               ";
  PRINT AT(40,12);
  FORM INPUT 2,a$
  PRINT AT(42,12);"/";
  FORM INPUT 2,b$
  PRINT AT(45,12);"/";
  FORM INPUT 2,c$
  ' EINGEGEBENES DATUM ALS SYSTEMDATUM ANMELDEN
  DATE$=a$+"."+b$+"."+c$
  GOSUB desktop
RETURN
