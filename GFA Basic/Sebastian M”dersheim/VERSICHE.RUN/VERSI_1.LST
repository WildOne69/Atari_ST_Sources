' Accessory "RETTER" des Datenrettungssystems      
' by Sebastian Mîdersheim           
' (c) 1991 MAXON Computer
'                                                   
RESERVE 33000       ! wir brauchen etwas Platz    
ON BREAK CONT       ! Abbruch nicht mîglich       
DIM puffer%(3)      ! 4 Longwords = 16 Bytes      
eintrag$="  Datenrettung" ! Accessory anmelden  
ap_id%=APPL_INIT()  ! Dazu brauchen wir unsere
'                     Identifikationsnummer
REM ap_id%=Dpeek(Lpeek(gb+4)+4) 
dummy%= MENU_REGISTER(ap_id%,eintrag$)
REM DPOKE GINTIN,DPEEK(LPEEK(GB+4)+4)                 
REM LPOKE ADDRIN,VARPTR(eintrag$)                    
REM GEMSYS 35
'
DO
  ~EVNT_MESAG(V:puffer%(0)) ! Auf Aufruf und 
  '                           Daten warten
  REM LPOKE ADDRIN,VARPTR(puffer%(0))
  REM GEMSYS 23
  ' Wort 0: Nachrichtentyp (muû hier 40 oder 42 
  '         sein)
  '         40: Accessory wurde angeklickt
  '         42: Ein Programm will uns etwas Åber-
  '             geben
  ' Wort 1: Identifikationsnummer des sendenden 
  '         Programmes
  ' Wort 2: <>0 bedeutet, die Nachricht hat öber-
  '         lÑnge
  IF (puffer%(0) DIV 65536)=40 THEN ! Accessory 
    @open_ac        ! wurde angeklickt
  ENDIF             
  IF (puffer%(0) DIV 65536)=42 THEN ! Daten werden
    @get_datas      ! Åbergeben
  ENDIF
LOOP
'
PROCEDURE open_ac
  IF begin%=0 ! Wenn Daten unkorrekt oder noch gar
    '                       nicht Åbergeben wurden
    ALERT 1,"Hier Datenretter.|Mir liegen keine|korrekten Daten vor",1," ... ",dummy%
  ELSE
    ALERT 1,"Hier Datenretter.|Ich kann Ihre|Daten jetzt speichern",1,"Na los|Halt",dummy%
    IF dummy%=1
      BSAVE "\Daten.asc",begin%,laenge%
    ENDIF
  ENDIF
RETURN
'
PROCEDURE get_datas
  begin%=puffer%(2)
  laenge%=puffer%(3)
  ' Wort 3+4 (=ein Langwort): Startadresse
  ' Wort 5+6                : LÑnge
  IF begin%=0 OR laenge%=0  !Da kann was nicht      
    '                        stimmen ...
    ALERT 1,"Hier Datenretter:|Habe fehlerhafte Daten|erhalten",1," Aha? ",dummy%
    begin%=0
  ELSE
    ALERT 1,"Hier Datenretter:|Habe korrekte Daten|erhalten",1," Schîn ",dummy%
  ENDIF
RETURN
