' ****************** LISTING 2 ******************
' *    Baeume ausgeben und wieder entfernen     *
' *    -------------------------------------    *
' *    Sprache:  GFA-BASIC V3.x                 *
' *    Autor:    Volker Goreth, 4150 Krefeld    *
' *    Datum:    22.12.90                       *
' *    -------------------------------------    *
' *       (c) 1991 by MAXON Computer GmbH       *
' ***********************************************
'
' Objektbaum auf Bildschirm ausgeben:
PROCEDURE draw_tree(l.tno&,l.depth&,l.can&,l.ok&,l.c!)
  '    --> l.tno&        !Baum-Nr.
  '    --> l.depth&      !Anzahl der Ebenen
  '    --> l.can&,l.ok&  !Buttons in Normal-
  '                      ! darstellung
  '    --> l.c!          !Baum zentriert
  ' GLOBAL obj_adr%      !Baum-Adresse
  '        obj_x&,obj_y& !Objekt-X- u. Y-Koord.
  '        obj_w&,obj_h& !Objekt-Breite u. -Hoehe
  '        x_res&,y_res& !X- und Y-Aufloesung
  '                      ! (in Pixel)
  '
  '  !!! Wenn Objektbaeume nicht zentriert !!!
  '  !!! ausgegeben werden,   m u s s   in !!!
  '  !!! x_res& und y_res& die Bildschirm- !!!
  '  !!!    Aufloesung angegeben werden    !!!
  '  !!!  (siehe Listing 3: Beispielprg.)  !!!
  '
  ~WIND_UPDATE(1)        !Menues sperren
  ' Objektadresse von Baum-Nr. l.tno& holen:
  ~RSRC_GADDR(0,l.tno&,obj_adr%)
  '
  ' Abbruch-Feld in Normaldarstellung
  '  (-1, wenn nicht vorhanden):
  IF l.can&>-1
    OB_STATE(obj_adr%,l.can&)=0
  ENDIF
  ' OK-Feld in Normaldarstellung:
  IF l.ok&>-1
    OB_STATE(obj_adr%,l.ok&)=0
  ENDIF
  '
  ' Den Koordinaten- und Groessen-Variablen
  ' Werte fuer Objektbaumdarstellung
  ' in Bildschirmmitte zuweisen:
  ~FORM_CENTER(obj_adr%,obj_x&,obj_y&,obj_w&,obj_h&)
  '
  ' Koordinaten ggf. korrigieren, wenn der Baum
  ' nicht zentriert, sondern an Mausposition
  ' ausgegeben werden soll (l.c!=FALSE):
  IF NOT l.c!
    ' Rand links, rechts und unten: mind. 3 Pixel
    obj_x&=MAX(5,MIN(MOUSEX-(obj_w&\2),x_res&-obj_w&-3))
    ' bzw. mind. 20 Pixel unter Bildschirm-
    ' Oberkante (=Platz fuer Menue-Leiste):
    obj_y&=MAX(20,MIN(MOUSEY-(obj_h&\2),y_res&-obj_h&-3))
    OB_X(obj_adr%,0)=obj_x&+3
    OB_Y(obj_adr%,0)=obj_y&+3
  ENDIF
  '
  ' Bildschirmbereich reservieren:
  ~FORM_DIAL(0,0,0,0,0,obj_x&,obj_y&,obj_w&,obj_h&)
  ' Sich ausdehnendes Rechteck zeichnen:
  ~FORM_DIAL(1,MOUSEX,MOUSEY,16,16,obj_x&,obj_y&,obj_w&,obj_h&)
  '
  ' Objektbaum zeichnen:
  ~OBJC_DRAW(obj_adr%,0,l.depth&,obj_x&,obj_y&,obj_w&,obj_h&)
  '
  ~WIND_UPDATE(0)       !Menues wieder freigeben
RETURN
'
' Objektbaum vom Bildschirm entfernen:
PROCEDURE redraw_tree
  ~WIND_UPDATE(1)       !Menues sperren
  ' "Schrumpfendes" Rechteck zeichnen:
  ~FORM_DIAL(2,MOUSEX,MOUSEY,16,16,obj_x&,obj_y&,obj_w&,obj_h&)
  ' Reservierten Bildschirmbereich freigeben:
  ~FORM_DIAL(3,0,0,0,0,obj_x&,obj_y&,obj_w&,obj_h&)
  ON MENU               !(wg. redraw_msg)
  ~WIND_UPDATE(0)       !Menues wieder freigeben
RETURN
