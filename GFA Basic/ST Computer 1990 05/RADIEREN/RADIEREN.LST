DIM m$(18)                                               !MenÅ erstellen
RESTORE mmenue
FOR z%=0 TO 17
  READ m$(z%)
NEXT z%
mmenue:
DATA DESK  ,  INFO,--------------------
DATA 1,2,3,4,5,6,""
DATA DEMO  ,  Zeichnen  ,  Radieren  ,-------------,  Quit,"","",""
'
MENU m$()
@menu
'
PROCEDURE menu
  DO                                                       !Auf Ereignis
    ON MENU                                                !warten
    ON MENU  GOSUB auswahl                                 !und verzweigen
  LOOP
RETURN
'
PROCEDURE auswahl                                          !Auswahl treffen
  WHILE MOUSEK<>0
  WEND
  MENU OFF
  IF MENU(0)=1
    @copyright
  ENDIF
  IF MENU(0)=11
    @zeichnen
  ENDIF
  IF MENU(0)=12
    @radieren
  ENDIF
  IF MENU(0)=14
    END
  ENDIF
RETURN
'
PROCEDURE radieren
  REPEAT
    ON MENU
  UNTIL MOUSEK=0
  DEFMOUSE 5                              ! Fadenkreuz
  xx%=10                                  ! Linke-obere Ecke
  yy%=60                                  ! der Radierbox und
  @set_m(20,90)                           ! Default fÅr rechte-untere Ecke
  GRAPHMODE 3
  WHILE MOUSEK=0                          ! Rahmen-Schleife
    MOUSE x%,y%,k%
    '
    IF x%<xx%                             !Keine "negativen" Werte
      @set_m(xx%,y%)                      !fÅr rechte-untere Ecke
    ENDIF
    IF y%<yy%
      @set_m(x%,yy%)
    ENDIF
    '
    BOX xx%,yy%,x%,y%                          ! Radierbox zeigen
    REPEAT                                     ! bis Maustaste losgelassen
    UNTIL MOUSEK=1 OR MOUSEX<>x% OR MOUSEY<>y% ! oder Maus bewegt
    BOX xx%,yy%,x%,y%                          ! Radierbox lîschen
  WEND
  GET xx%,yy%,x%,y%,sav$                       ! Hintergrund retten
  GRAPHMODE 1                                  ! und
  DEFFILL 0                                    ! fÅr
  PBOX xx%+1,yy%+1,x%-1,y%-1                   ! Radierbox sÑubern
  DEFFILL 1,1
  BOX xx%,yy%,x%,y%                            ! Radierbox endgÅltig zeichnen
  GET xx%,yy%,x%,y%,rad$                       ! und puffern
  GRAPHMODE 3
  BOX xx%,yy%,x%,y%                            ! Radierbox lîschen und
  PUT xx%,yy%,sav$                             ! Hintergrund restaurieren
  '
  xd%=x%-xx%                                   ! Boxmaûe fÅr
  yd%=y%-yy%                                   ! Offset berechnen
  @set_m(300,199)                              ! Radierbox in Bildmitte
  '
  WHILE MOUSEK<>0
  WEND
  DO                                                !Radierschleife
    WHILE MOUSEK=0                                  !Verschiebeschleife
      MOUSE x%,y%,k%
      HIDEM
      SGET bild$                                    ! Hintergrund puffern
      PUT x%,y%,rad$,6                              ! Radierbox zeigen
      WHILE MOUSEX=x% AND MOUSEY=y% AND MOUSEK=0    ! warten
        HIDEM
        IF INKEY$=CHR$(0)+CHR$(97)                  ! UNDO
          PUT x%,y%,rad$,6                          ! Radierbox lîschen
          PUT ux%,uy%,und$                          ! Bild restaurieren
          SGET bild$                                ! Hintergrund puffern
          PUT x%,y%,rad$,6                          ! Radierbox zeigen
        ENDIF
      WEND
      SPUT bild$                            !Hintergrund Åber Radierbox legen
    WEND
    '
    WHILE MOUSEK=1 AND MOUSEY>20            !Wenn linke MT gedrÅckt und
      MOUSE ux%,uy%,k%                      !Radierbox auûerhalb der
      HIDEM                                 !MenÅ-Leiste
      DEFFILL 0
      GRAPHMODE 1
      GET ux%,uy%,ux%+xd%,uy%+yd%,und$     !FlÑche fÅr UNDO puffern
      PBOX ux%,uy%,ux%+xd%,uy%+yd%         !und lîschen
      DEFFILL 1,1,
      WHILE MOUSEK=1
      WEND
    WEND
    EXIT IF MOUSEK=2
  LOOP
  REPEAT
    ON MENU
  UNTIL MOUSEK=0
  SHOWM
  DEFMOUSE 0
  DEFFILL 1,1,
  GRAPHMODE 1
RETURN
'
PROCEDURE copyright
  SGET bild$
  GRAPHMODE 1
  DEFFILL 0
  PBOX 80,74,570,296
  DEFFILL 1
  BOX 80,74,570,296
  BOX 84,80,566,290
  DEFTEXT ,,,26
  TEXT 165,136,"  R a d i e r e n  "
  DEFTEXT ,,,13
  TEXT 230,170,"in  GfA-Basic (V.2.02)"
  TEXT 240,270," Hans-H.Ackermann "
  DEFTEXT ,,,6
  TEXT 285,280," 1989 "
  TEXT 234,268,CHR$(189)
  REPEAT
  UNTIL MOUSEK<>0
  SHOWM
  SPUT bild$
  @menu
RETURN
'
PROCEDURE zeichnen      !Demo-Graphik
  ERASE f$()
  DIM f$(4)
  BOX 80,130,570,280
  DEFTEXT ,,,13
  t$="Radieren mit der Maus"
  FOR z%=0 TO 3
    TEXT 120+(40*z%),154+(38*z%),t$
  NEXT z%
  FOR z%=o TO 4
    PBOX 320+(45*z%),136+(24*z%),350+(45*z%),170+(24*z%)
    DEFFILL ,2,RANDOM(24)
  NEXT z%
  PBOX 80,30,570,128
  PBOX 80,282,570,377
RETURN
'
PROCEDURE set_m(xm%,ym%)  !Mauszeiger setzen
  hand%=DPEEK(CONTRL+12)
  DPOKE CONTRL+2,0
  DPOKE CONTRL+6,2
  DPOKE CONTRL+12,hand%
  DPOKE INTIN,1
  DPOKE INTIN+2,2
  VDISYS 33
  DPOKE PTSIN,xm%
  DPOKE PTSIN+2,ym%
  DPOKE CONTRL+2,1
  DPOKE CONTRL+6,0
  DPOKE CONTRL+12,hand%
  VDISYS 28
RETURN
'
