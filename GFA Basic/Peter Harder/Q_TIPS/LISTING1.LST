'(c)1994 by MAXON-Computer
'Autor: Peter Harder
'
FUNCTION scrap_read$
  ' RÅckgabeformat: "C:\CLIPBRD\"
  LOCAL scrap$,drive$,back&
  '
  ~SCRP_READ(scrap$)
  IF LEN(scrap$)=0
  ' Clipboard noch nicht gesetzt?
    scrap$="\"
  ELSE IF RIGHT$(scrap$)<>"\"
  ' hÑngt CHR$(0) noch dran?
    scrap$=LEFT$(scrap$,INSTR(scrap$,CHR$(0))-1)
  ' dann weg damit
  ENDIF
  '
  IF (NOT @exist_ordner(LEFT$(scrap$,LEN(scrap$)-1))) OR LEN(scrap$)<11
    IF @exist_drive("C:")
      drive$="C:"
    ELSE
      drive$="A:"
    ENDIF
    IF NOT @exist_ordner(drive$+"\CLIPBRD")
      MKDIR drive$+"\CLIPBRD"
    ENDIF
    '
    back&=SCRP_WRITE(drive$+"\CLIPBRD\"+CHR$(0))
    IF back&<>0
      scrap$=drive$+"\CLIPBRD\"
    ELSE
      scrap$=""
    ENDIF
  ENDIF
  '
  RETURN scrap$
  '
ENDFUNC
'
FUNCTION exist_ordner(ordner$)
  ~FSETDTA(BASEPAGE+128)
  ' Setzen der Disktransferadresse
  IF FSFIRST(ordner$+CHR$(0),&X10000)=0
  ' Dateisuche mit gesetztem Ordnerbit,
    RETURN TRUE
  ' findet aber auch normale Dateien
  ELSE
    RETURN FALSE
  ENDIF
ENDFUNC
'
FUNCTION exist_drive(l_pfad$)
  LOCAL drive!,drives%,byte&
  l_pfad$=UPPER$(l_pfad$)
  '
  IF MID$(l_pfad$,2,1)=":" OR LEN(l_pfad$)=1
    byte&=ASC(l_pfad$)-65
    drives%=GEMDOS(14,GEMDOS(25))
    ' laut Profibuch S 223
    IF BTST(drives%,byte&)=TRUE
      drive!=TRUE
    ELSE
      drive!=FALSE
    ENDIF
  ELSE
    drive!=TRUE
  ENDIF
  '
  RETURN drive!
  '
ENDFUNC

