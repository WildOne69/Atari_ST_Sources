' Text-Hardcopy
'(c) 1992 MAXON Computer
'
$m 33000             ! Speicher reservieren
ap_id&=APPL_INIT()
'
' Programm lÑuft nur in der hohen Auflîsung (640x400) und mit
' dem normalen Systemzeichensatz (8x16) Pixel. Attribute, wie 
' fett, kursiv etc. werden nicht erkannt...
'
' Das Programm kann auf 3 Arten gestartet werden :
' 1. in der MenÅleiste anwÑhlen
' 2. durch DrÅcken und Halten der beiden Shift-Tasten
     (ca. 1 sec.)
' 3. durch Umbennen in *.PRG und Starten von einer Shell
'
CLR charset$
GET 20,0,40,20,backgr$
FOR t|=0 TO 158             ! Zeichentabelle initialisieren
  TEXT 30,13,CHR$(t|)       ! Diese Routine wird nur einmal
  GET 30,0,36,15,char$      ! beim Booten des Rechners bzw. beim
  charset$=charset$+char$   ! Start als PRG durchlaufen!
NEXT t|                     ! Die Obergrenze (ASCII 158) kann
PUT 20,0,backgr$            ! heraufgesetzt werden. Scan-Vorgang
CLR backgr$                 ! dauert dann aber lÑnger!
IF ap_id&<>0
  me_id&=MENU_REGISTER(ap_id&,"  Texthardcopy ")
  DO
    b$=SPACE$(16)                       ! Ereignispuffer
    a%=VARPTR(b$)
    '
    ' Timer event (1 sek.) setzen und auf Ereignis warten
    '
    ~EVNT_MULTI(48,0,0,0,0,0,0,0,0,0,0,0,0,0,a%,1000)
    '
    ' MenÅleiste abfragen
    IF ASC(MID$(b$,2,1))=40  ! Accessory angeklickt?
      hardcpy
    ENDIF
    '
    r%=BIOS(11,-1)           ! Shift-Tasten gedrÅckt?
    IF (r% AND 3)=3
      hardcpy
    ENDIF
  LOOP                       ! Acc's enden nie ...
ELSE
  hardcpy                    ! oder als PRG gestartet?
  END
ENDIF
PROCEDURE hardcpy
  SHOWM
  CLR box_wide,snap!,anz_char%,txt$
  ' erst mal ein paar allgemeine Fragen stellen ...
  ALERT 2,"Texthardcopy by A.Lauterbach|Wovon soll eine 
  Text-Hardcopy|angefertigt werden ?",1," GEM | TOS | Help ",d%
  SELECT d%
  CASE 1
    add_y%=3
  CASE 2
    add_y%=1
  CASE 3
    ALERT 1,"GEM - Hardcopy von Window|TOS - Hardcopy von TOS-Screen|Select - Zeilenabstand wÑhlen|Snap - Snapshotutility",1,"Okay|Select|Snap",d%
    IF d%=2
      REPEAT
        ALERT 1,"Zeilenabstand wÑhlen| |Abstand = "+STR$(15+add_y%),2," << |Okay| >> ",d%
        SELECT d%
        CASE 1
          add_y%=MAX(add_y%-1,0)
        CASE 3
          add_y%=MIN(add_y%+1,5)
        ENDSELECT
      UNTIL d%=2
    ELSE IF d%=3
      snap!=TRUE          !  Snapshot-Utility
    ENDIF
  ENDSELECT
  DEFMOUSE 3
  ' Rubberband darstellen
  GET 400,0,639,20,backgr$
  TEXT 450,15,"Bereich wÑhlen"
  REPEAT
    MOUSE xa%,ya%,mk%
  UNTIL mk%<>0
  GRAPHMODE 3
  nxa%=xa%
  nya%=ya%
  REPEAT
    MOUSE xe%,ye%,mk%
    IF ((xe%<>nxa%)+(ye%<>nya%))*(xe%>xa%)*(ye%>ya%)
      BOX xa%,ya%,xe%,ye%
      BOX xa%,ya%,nxa%,nya%
      nxa%=xe%
      nya%=ye%
    ENDIF
  UNTIL mk%=0
  '
  BOX xa%,ya%,xe%,ye%
  IF snap!=TRUE                ! Snapshot-Utility
    GET xa%,ya%,xe%,ye%,graf$
    HIDEM
    f$="HRDCPY"
    vernr%=1
    WHILE EXIST(f$+STR$(vernr%)+".OBJ") ! und die FrÅchte der
      INC vernr%                        ! Arbeit verewigen
    WEND
    OPEN "O",#1,f$+STR$(vernr%)+".OBJ"
    PRINT #1,graf$;
    CLOSE #1
    SHOWM
  ELSE
    GRAPHMODE 0
    BOX 600,2,632,18
    HIDEM
    ' Textanfang suchen (dauert etwa 1-10 sec.)
    TEXT 450,15,"Suche Textanfang"
    ya%=MAX(ya%-3,0)
    FOR x_ofs%=xa% TO xa%+15+add_y%
      PBOX 616+(xa%-x_ofs%),2,616-(xa%-x_ofs%),18
      FOR y_ofs%=ya% TO ya%+15+add_y%
        GET x_ofs%,y_ofs%,x_ofs%+6,y_ofs%+15,char$
        char%=INSTR(charset$,char$) DIV 38  ! bekanntes Textmuster?
        EXIT IF char%<>0 AND char%<>32      ! Text gefunden
      NEXT y_ofs%
      EXIT IF char%<>0 AND char%<>32  ! und raus hier ..
    NEXT x_ofs%
    start_x%=x_ofs%                   ! Textanfang
    TEXT 450,15,"      Scanne Text"   ! jetzt geht's los
    DEFFILL 1,0
    PBOX 600,2,632,18
    DEFFILL 1,2,8
    REPEAT
      REPEAT
        PBOX 617+box_wide,2,616-box_wide,18          ! eine Zeile scannen
        GET x_ofs%,y_ofs%,x_ofs%+6,y_ofs%+15,char$
        char%=INSTR(charset$,char$) DIV 38           ! Zeichen bekannt?
        IF char%>0
          INC anz_char%
          txt$=txt$+CHR$(char%)  ! Zeichen merken
        ELSE
          txt$=txt$+" "
        ENDIF
        ADD x_ofs%,8
      UNTIL x_ofs%>xe%           ! Zeilenende?
      x_ofs%=start_x%
      ADD y_ofs%,15+add_y%       ! neue Zeile suchen
      ADD box_wide,(15+add_y%)/((ye%-ya%)/(15+add_y%))
      txt$=txt$+CHR$(13)+CHR$(10)
    UNTIL y_ofs%>ye%             ! fertig ...
    DEFMOUSE 0
    SHOWM
    PUT 400,0,backgr$            ! Bild restaurieren
    ALERT 2,"Es wurden "+STR$(anz_char%)+" Zeichen|erkannt! Wohin damit ?",1,"Drucker|Disk|MÅll",d%
    IF d%=1
      LPRINT txt$
    ELSE IF d%=2
      f$="HRDCPY"
      vernr%=1
      WHILE EXIST(f$+STR$(vernr%)+".TXT") ! und die FrÅchte der
        INC vernr%                        ! Arbeit verewigen
      WEND
      OPEN "O",#1,f$+STR$(vernr%)+".TXT"
      PRINT #1,txt$
      CLOSE #1
    ENDIF
  ENDIF
  CLR backgr$,graf$
RETURN
