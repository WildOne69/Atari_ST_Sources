' **********************************************
' * polygon_triangle - (testprogramm) 29.03.90 *
' *                    in  gfa-basic v3.07     *
' * ingo weirauch      (c) 1992 MAXON Computer *
' **********************************************
'
max_p&=200                 ! maximale punktzahl
'
INLINE adr%,326
p_tri%=adr%                ! polygon -> dreiecke
test_p%=adr%+4             ! polygon ueberwachen
test_tri%=adr%+8           ! ein dreieck testen
'
EVERY 25 GOSUB taste
DO
  CLS
  GRAPHMODE 3
  PRINT AT(1,1);CHR$(27);"p";SPACE$(80)
  PRINT AT(10,1);"GEBEN SIE EIN POLYGON OHNE ";
  PRINT "šBERSCHNEIDUNGEN IM UHRZEIGERSINN EIN"
  PRINT AT(1,25);CHR$(27);"w[ESC = ENDE] ";
  PRINT " [LINKE MAUSTASTE = POLYGOPUNKT] ";
  PRINT " [RECHTE MAUSTASTE = POLYGONENDE] ";
  polyline
  anz_d&=anz_e&-2
  ERASE tri&()
  DIM tri&(5,anz_e&)
  a$=SPACE$((anz_e&+3)*4)      ! puffer
  tri%=V:tri&(0,0)             ! dreiecksliste
  x%=V:x&(0)                   ! x-koordinaten
  y%=V:y&(0)                   ! y-koordinaten
  e&=C:p_tri%(L:tri%,L:V:a$,L:x%,L:y%,anz_e&)
  IF e&=-1
    '
    CLR sf
    FOR i&=0 TO anz_d&
      x0&=tri&(0,i&)
      y0&=tri&(1,i&)
      x1&=tri&(2,i&)
      y1&=tri&(3,i&)
      x2&=tri&(4,i&)
      y2&=tri&(5,i&)
      a=SQR((x0&-x1&)^2+(y0&-y1&)^2)
      b=SQR((x1&-x2&)^2+(y1&-y2&)^2)
      c=SQR((x2&-x0&)^2+(y2&-y0&)^2)
      s=(a+b+c)*0.5
      f=SQR(s*(s-a)*(s-b)*(s-c))
      ADD sf,f
    NEXT i&
    p=sf*100/((320*200)*BSET(0,XBIOS(4)))
    PRINT AT(1,1);" DIE POLYGONFLŽCHE NIMMT ";INT(p*100)/100;" % DER ";
    PRINT "BILDSCHIRMOBERFLŽCHE EIN";SPACE$(20)
    MOUSE ox&,oy&,k&
    REPEAT
      MOUSE x&,y&,k&
    UNTIL k&=1 OR ABS(x&-ox&)>20 OR ABS(y&-oy&)>20
    '
    PRINT AT(1,1);"ERR: ";e&;SPACE$(70)
    PRINT AT(12,1);"DIE DREIECKSNUMMER LAUTET:"
    REPEAT
    UNTIL MOUSEK=0
    nn&=-1
    '
    REPEAT
      MOUSE mx&,my&,k&
      tri%=V:tri&(0,0)
      IF C:test_p%(L:tri%,L:*n&,anz_d&,mx&,my&)
        IF n&<>nn& OR anz_d&=0
          PRINT AT(39,1);n&;"        "
          GRAPHMODE (k&+1)*3
          IF nn&>-1
            draw_tri(nn&)
          ENDIF
          nn&=n&
          draw_tri(n&)
        ENDIF
        nh&=n&
      ELSE IF nh&<>n&
        nh&=n&
        PRINT AT(39,1);"AUžERHALB"
      ENDIF
    UNTIL k&=2
  ELSE
    CLS
    PRINT AT(30,12);"EINGABEFEHLER !!!!"
    PRINT AT(1,14);"?? ";
    PRINT "FALSCHER UMLAUFSINN / ";
    PRINT "šBERSCHNEIDUNGEN / ";
    PRINT "DOPPELTE PUNKTE (Z.B. START=ENDE) ???"
    ~INP(2)
  ENDIF
LOOP
'
PROCEDURE draw_tri(x&)
  ERASE xh&(),yh&()
  DIM xh&(2),yh&(2)
  FOR i&=0 TO 2
    xh&(i&)=tri&(i&*2,x&)
    yh&(i&)=tri&(i&*2+1,x&)
  NEXT i&
  POLYFILL 3,xh&(),yh&()
RETURN
'
PROCEDURE polyline
  LOCAL n&,m&,z&,key&,t&
  ERASE x&(),y&()
  DIM x&(max_p&),y&(max_p&)
  CLR z&
  REPEAT
    MOUSE x&(z&),y&(z&),k&
  UNTIL k&=1
  n&=x&(z&)
  m&=y&(z&)
  REPEAT
    INC z&
    key&=1
    REPEAT
      REPEAT
        MOUSE x&(z&),y&(z&),k&
        IF x&(z&)<>n& OR y&(z&)<>m&
          LINE x&(z&-1),y&(z&-1),n&,m&
          LINE x&(z&-1),y&(z&-1),x&(z&),y&(z&)
          PRINT AT(1,1);"E: ";z&
          SHOWM
          n&=x&(z&)
          m&=y&(z&)
        ENDIF
      UNTIL k&<key&
      key&=3
    UNTIL k&>0
  UNTIL z&=max_p& OR k&=2
  DEC z&
  DRAW  TO x&(z&),y&(z&) TO x&(0),y&(0)
  anz_e&=z&
RETURN
'
PROCEDURE taste
  IF ASC(INKEY$)=27
    PRINT CHR$(27);"q";CHR$(27);"v"
    GRAPHMODE 1
    END
    EDIT
  ENDIF
RETURN
