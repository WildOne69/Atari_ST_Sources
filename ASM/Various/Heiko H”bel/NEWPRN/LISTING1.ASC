;NEWPRN.PRG
;Beschleunigung der Druckerausgabe an der Parallelschnittstelle
;Åber die Funktion BIOS #3, GerÑt 0 (Printer) und GEMDOS #5
;Programm zum einmaligen Start (letztes Programm im AUTO-Ordner)
;by Heinrich Emmerl  (c) 1992 MAXON Computer
xbios     equ 14
bios      equ 13
gemdos    equ 1
psg_r     equ $ff8800                   ;PSG-Read-Register
psg_w     equ $ff8802                   ;PSG-Write-Register
gpip      equ $fffa01                   ;MFP-Data-Register (Bit 0 = 'BUSY')
;
;***** Exception-Vektoren 'verbiegen'
;
start:    lea.l     newbios(pc),a1      ;neue BIOS-Einsprungadresse
          pea       (a1)
          move.w    #45,-(sp)           ;Vektor-Nummer
          move.w    #5,-(sp)            ;setexec
          trap      #bios
          addq.l    #8,sp
          move.l    d0,oldbv            ;alten BIOS-Vektor merken
;
          lea.l     newgem(pc),a1       ;neue GEMDOS-Einsprungadresse
          pea       (a1)
          move.w    #33,-(sp)           ;Vektor-Nummer
          move.w    #5,-(sp)            ;setexec
          trap      #bios
          addq.l    #8,sp
          move.l    d0,oldgv
;
          move.l    #ende-start+256,-(sp)
          move.w    #$31,-(sp)          ;keep process
          trap      #gemdos             ;Programm resident halten
;
;***** Hier beginnt die neue GEMDOS-5-Routine
;
newgem:   move.w    (sp),d0
          move.l    usp,a0
          btst.l    #13,d0
          beq.s     newgem1
          lea.l     6(sp),a0
newgem1:  cmpi.w    #5,(a0)+            ;ist es unsere Funktions-Nr. 5?
          beq.s     newprout
          movea.l   oldgv,a0            ;weiter im Original-GEMDOS
          jmp       (a0)
;
;***** Hier beginnt die neue BIOS-3-Routine
;
newbios:  move.w    (sp),d0
          move.l    usp,a0
          btst.l    #13,d0
          beq.s     newbios1
          lea.l     6(sp),a0
newbios1: cmpi.w    #3,(a0)+            ;ist es unsere Funktions-Nr. 3?
          bne.s     nein
          cmpi.w    #0,(a0)+            ;ist es GerÑt 0 (Drucker)?
          beq.s     newprout
nein:     movea.l   oldbv,a0            ;weiter im Original-BIOS
          jmp       (a0)
;
;***** Hier beginnt die neue Zeichenausgabe an Centronics
;
newprout: move.l    #2000000,t_out      ;ZÑhler fÅr timeout
          move.l    #-1,d0              ;falls kein Fehler
timeout:  btst.b    #0,gpip             ;BUSY schon LOW?
          beq.s     out                 ;ja!
          subq.l    #1,t_out            ;nein! weiter warten
          bne.s     timeout
          clr.l     d0                  ;Fehlerflag!
          rte       
out:      move.b    #15,psg_r           ;Register IOB (Soundchip) ansprechen
          move.b    1(a0),psg_w         ;Datum an Centronics anlegen
          move.b    #14,psg_r           ;Register IOA ansprechen
          bclr.b    #5,psg_w            ;Strobe-Impuls ausgeben...
          bset.b    #5,psg_w            ;...und gleich wieder wegnehmen
          rte                           ;zurÅck aus der Exception
;
          .EVEN 
oldgv:    .DS.l 1   ;Platz zum Merken des alten GEMDOS-Vektors
oldbv:    .DS.l 1   ;Platz zum Merken des alten BIOS-Vektors
t_out:    .DS.l 1   ;ZÑhler fÅr Drucker-Timeout
ende:     .EVEN
