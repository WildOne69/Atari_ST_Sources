
 ' SUBMENUE Demo fÅr den FALCON - (RCS erforerlich)
 ' Version 1.0 fÅr ST Computer - Februar/Juni '93
 ' (C) Kai Michael Speck, Solmser Weg 4, 35630 Ehringshausen
 ' Die Resourcen wurden mit dem INTERFACE-RCS erstellt

 COMPILER "NOEX"' (* nur fÅr MULTI-TOS erforderlich *)
 COMPILER "FLAGS 11; CUTLIB"' (* FASTLOAD, GLOBAL *)

 LIBRARY Gem ,"GEM.LIB"' (* am Besten ab Version 3.0 *)
 LIBRARY Gem33 ,"GEM33.LIB"
 LIBRARY Popup ,"POPUP_11.LIB"
 LIBRARY Object ,"OBJECT.LIB"

 CLEAR FRE(0)-(64*1024)' (* Alles bis auf 64KB zurÅck ans GEMDOS *)

 Appl_Init' (* Programm anmelden *)

 COMPILER "DEF_CONST"
   None%=0:Selectable%=1:Default%=2:Exit%=4:Lastob%=$20:Touchexit%=$40
   Normal%=0:Checked%=4:Disabled%=8:Outlined%=$10:Shadowed%=$20:Draw3d%L=$80
   R_Tree%=0:Mn_Selected%=10
   ' (* Resource Datei Indizes fÅr SUBMENU *)
   Menubar%=0' Menuebaum
     M_Info%=7'  STRING in Baum MENUBAR
     M_Neu%=16'  STRING in Baum MENUBAR
     M_Laden%=18'  STRING in Baum MENUBAR
     M_Sichern%=19'  STRING in Baum MENUBAR
     M_Ende%=21'  STRING in Baum MENUBAR
   Submenu%=1' Formular/Dialog
     Sub_Box%=0'  BOX in Baum SUBMENU
       S_Img%=1'  STRING in Baum SUBMENU
       S_Iff%=3'  STRING in Baum SUBMENU
       S_Tif%=4'  STRING in Baum SUBMENU
       S_Gif%=6'  STRING in Baum SUBMENU
 COMPILER "END_CONST"

 Rsrc_Load("SUBMENU.RSC",Ret%L)
 IF Ret%L THEN ' (* Original RSC-Datei vorhanden *)
   Rsrc_Gaddr(R_Tree%,Menubar%,Menu_Tree%L)' (* Der MenÅbaum *)
   Rsrc_Gaddr(R_Tree%,Submenu%,Sub_Tree%L)' (* Das SubmenÅ *)
 ELSE
   FORM_ALERT (1,"[3][|Resourcedatei nicht gefunden  ][OK]")
   GOTO Programmende
 ENDIF

 Menu_Bar(Menu_Tree%L)

 Struct_Menu(Sub_Str%L,Sub_Tree%L,Sub_Box%,S_Iff%,0,0)' (* Struktur fÅr das SubmenÅ *)
 Menu_Attach(1,Menu_Tree%L,M_Sichern%,Sub_Str%L)' (* SubmenÅ mit MenÅbaum verbinden *)

 DIM Mbuf%(7)
 REPEAT
   Evnt_Mesag(Mbuf$)
   FOR I%L=0 TO 7
     Mbuf%(I%L)= CVI( MID$(Mbuf$,1+I%L*2))
   NEXT I%L
   IF Mbuf%(0)=Mn_Selected% THEN
     SELECT Mbuf%(4)
       CASE M_Info%
         FORM_ALERT (1,"[0][|FALCON SubmenÅ Demo  |fÅr ST Computer 8/93|  (C) AZH '93][OK]")
       CASE M_Sichern%
         FORM_ALERT (1,"[0][|Es wurde das HauptmenÅ|'Sichern als' angewÑhlt  ][Weiter]")
       CASE S_Img% TO S_Gif%
         FORM_ALERT (1,"[0][|Es wurde ein SubmenÅ angewÑhlt  ][Weiter]")
     END_SELECT
     Menu_Tnormal Mbuf%(3),1
   ENDIF
 UNTIL Mbuf%(4)=M_Ende%

 Rsrc_Free' (* unbedingt den RSC-Speicher wieder freigeben *)

-Programmende
 Appl_Exit
 MOUSEON ' (* Im Moment noch fÅr MULTI-TOS erforderlich *)

 END

