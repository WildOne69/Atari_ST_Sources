 '+---------------------------------+
 '|           Listing 1             |
 '| DEN BILDSCHIRMSPEICHER IM GRIFF |
 '|       mit OMIKRON-BASIC         |
 '|           ANIMATION             |
 '+---------------------------------+

 CLEAR 360000:'GenÅgend Speicherplatz reservieren

 Bildschirm:'  Grundeinstellungen die mit der Auflîsung zu tun haben
 Erzeuge_Schirme 11

 SCREEN 1,Adr_Erster_Schirm%L:' Siehe Prozedur <Erzeuge_Schirme>
 Cursor_Aus:'Stîrt nur

 'Grafiken erzeugen

 X%L=Maxbreite%L\2:Y%L=Maxhoehe%L\2:'Kreismittelpunkt
 FOR I%L=2 TO 11

  'Text auf den ersten Schirm

   Aktiviere_Schirm 1:'Schirm 1 ist gerade sichtbar (SCREEN 1)
   LOCATE 2,1: PRINT "Bitte einen Moment Geduld"
   LOCATE 3,1: PRINT "Grafik Nr. ";I%L-1;" wird gerade erzeugt"

  'Grafik im Hintergrund auf die Schirme 2 bis 11

   Aktiviere_Schirm I%L:'Zur Zeit unsichtbar im Hintergrund
   LOCATE 2,1: PRINT "DEMO ANIMATION"
   FOR Radius%L=0 TO 400 STEP 20
     CIRCLE X%L,Y%L,Radius%L+2*I%L
   NEXT

 NEXT

 'ANIMATION

 SCREEN 2,Adr_Erster_Schirm%L
 Cursor_Aus
 REPEAT
   FOR I%L=2 TO 11
     Schirmadresse%L=Adr_Erster_Schirm%L+(I%L-1)*32000
     WAIT 1/40:'Bildfrequenz
     SCREEN 2,Schirmadresse%L
   NEXT
 UNTIL INKEY$ <>"":'Abbruch bei Tastendruck

 END


 'Standard-Prozeduren die fÅr die Demos benîtigt werden

 DEF PROC Bildschirm
   XBIOS (Aufloesung%L,4):'                 Bildschirmaufloesung ermitteln
   Maxbreite%L=640+320*(Aufloesung%L=0):'     Bildpunkte horizontal
   Maxhoehe%L=400+200*(Aufloesung%L<2):'      Bildpunkte vertikal
   CLIP 0,0,Maxbreite%L,Maxhoehe%L
   Bytes_Pro_Zeile%L=160+80*(Aufloesung%L=2)
 RETURN

 DEF PROC Cursor_Aus
   PRINT CHR$(27);"f";
 RETURN

 DEF PROC Erzeuge_Schirme(Nschirme%L)
   Adr_Erster_Schirm%L= MEMORY(32000*Nschirme%L+256)
   Adr_Erster_Schirm%L=(Adr_Erster_Schirm%L\256)*256+256
 RETURN

 DEF PROC Aktiviere_Schirm(Nr%L)
   LOCAL Adr%L
   Adr%L=Adr_Erster_Schirm%L+(Nr%L-1)*32000
   XBIOS (,5, HIGH(Adr%L), LOW(Adr%L),-1,-1,-1)
 RETURN

